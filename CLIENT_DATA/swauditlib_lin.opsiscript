; Copyright (c) uib GmbH <info@uib.de>
; License: AGPL-3.0

encoding=utf8

DefFunc getswdetails4_os() : void
	init_vars()
	setloglevel=$Midloglevel$
	set $displayName$ = $distroOS$+" : "+$distroName$
	if not ($displayName$ = '')
		set $displayVersion$ = $distRelease$
		set $tmp$ = $distroarch$
		if ($tmp$ = "i386") or ($tmp$ = "i586") or ($tmp$ = "i686")
			set $architecture$ = "x86"
		endif
		if ($tmp$ = "amd64") or ($tmp$ = "x86_64")
			set $architecture$ = "x64"
		endif
		if ($tmp$ = "aarch64")
			set $architecture$ = "arm64"
		endif
		if ($tmp$ = "noarch") or ($tmp$ = "all")
			set $architecture$ = ""
		endif
		set $winSoftwareId$ = $displayName$
		create_hash4()
	endif
Endfunc


DefFunc getswdetails4_deb($aktkey$ : string) : void
	DefVar $listcount$
	setloglevel=$Midloglevel$
	if not ($aktkey$ = "")
		init_vars()
		set $resultlist$ = splitStringOnWhiteSpace($aktkey$)
		set $listcount$ = count($resultlist$)
		set $displayName$ = takeString(0,$resultlist$)
		; Get installation state
		set $tmp$ = ""
		if $listcount$ int>= "5"
			set $tmp$ = takeString(4,$resultlist$)
		endif
		; Do not include packages with state 'rc'
		if not ($displayName$ = '') and not($tmp$ = 'rc')
			if $listcount$ int>= "2"
				set $displayVersion$ = takeString(1,$resultlist$)
				if $listcount$ int>= "3"
					set $tmp$ = takeString(2,$resultlist$)
					if ($tmp$ = "i386") or ($tmp$ = "i586") or ($tmp$ = "i686")
						set $architecture$ = "x86"
					endif
					if ($tmp$ = "amd64") or ($tmp$ = "x86_64")
						set $architecture$ = "x64"
					endif
					if ($tmp$ = "noarch") or ($tmp$ = "all")
						set $architecture$ = ""
					endif
					if $listcount$ int>= "4"
						set $installSize$ = takeString(3,$resultlist$)
						if not(isNumber($installSize$))
							set $installSize$ = "-1"
						endif
					endif
				endif
			endif
			set $winSoftwareId$ = $displayName$

			create_hash4()
		endif
	endif
Endfunc

DefFunc getswdetails4_suse($aktkey$ : string) : void
	setloglevel=$Midloglevel$
	if not ($aktkey$ = "")
		init_vars()
		set $resultlist$ = splitStringOnWhiteSpace($aktkey$)
		if takeString(0,$resultlist$) = 'i'
			set $displayName$ = takeString(2,$resultlist$)
			set $displayVersion$ = takeString(6,$resultlist$)
			set $tmp$ = takeString(8,$resultlist$)
			if ($tmp$ = "i386") or ($tmp$ = "i586") or ($tmp$ = "i686")
				set $architecture$ = "x86"
			endif
			if ($tmp$ = "amd64") or ($tmp$ = "x86_64")
				set $architecture$ = "x64"
			endif
			if ($tmp$ = "noarch") or ($tmp$ = "all")
				set $architecture$ = ""
			endif
			set $winSoftwareId$ = $displayName$

			create_hash4()
		endif
	endif
Endfunc

DefFunc getswdetails4_redhat($aktkey$ : string) : void
	setloglevel=$Midloglevel$
	if not ($aktkey$ = "")
		init_vars()
		set $resultlist$ = splitStringOnWhiteSpace($aktkey$)
		set $displayName$ = takeString(0,$resultlist$)
		set $resultlist2$  = splitString($displayName$, ".")
		set $displayName$ = takeString(0,$resultlist2$)
		set $tmp$ = takeString(1,$resultlist2$)
		if ($tmp$ = "i386") or ($tmp$ = "i586") or ($tmp$ = "i686")
			set $architecture$ = "x86"
		endif
		if ($tmp$ = "amd64") or ($tmp$ = "x86_64")
			set $architecture$ = "x64"
		endif
		if ($tmp$ = "aarch64")
			set $architecture$ = "arm64"
		endif
		if ($tmp$ = "noarch") or ($tmp$ = "all")
			set $architecture$ = ""
		endif
		set $displayVersion$ = takeString(1,$resultlist$)
		set $winSoftwareId$ = $displayName$

		create_hash4()
	endif
Endfunc
